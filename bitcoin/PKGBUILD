developer='http://www.bitcoin.org/'
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=0.16.0
pkgrel=2
arch=('x86_64')
pkgdesc="Bitcoin is a peer-to-peer network based digital currency"
makedepends=('autoconf' 'automake' 'boost' 'qt5-base' 'qt5-tools' 'qrencode' 'miniupnpc' 'protobuf' 'zeromq')
depends=('boost-libs' 'libevent' 'miniupnpc' 'zeromq')
license=("custom:${pkgname}")
source=("https://github.com/bitcoin/bitcoin/archive/v$pkgver.tar.gz")
sha512sums=('a72461029550bd1da5c933e888a313ccbfc855537eb91a555b412a938f88e578f840b5a8bb2c7ec0f9cc970ddfc177189e1d0ef3e81682e69aa8859077a99b8d')
validpgpkeys=(71A3B16735405025D447E8F274810B012346C9A6)
backup=('etc/bitcoin/bitcoin.conf')
install=install
releasepage=('https://github.com/bitcoin/bitcoin/releases')

xxxprepare() {
    cd ${pkgbase}-${pkgver}
    patch -p0 < ${startdir}/issue-11847.patch
    # From https://github.com/bitcoin/bitcoin/pull/11847/files
}

build() {
    cd ${pkgbase}-${pkgver}
    ./autogen.sh
    ./configure --prefix=/usr --with-gui=qt5 --with-incompatible-bdb
    make
}

package() {
    mkdir -p ${pkgdir}/etc
    mkdir -p ${pkgdir}/usr/bin
    mkdir -p ${pkgdir}/usr/lib/systemd/system
    mkdir -p ${pkgdir}/usr/share/man/man1
    mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
    mkdir -p ${pkgdir}/ubos/lib/bitcoind

    install -m755 ${pkgbase}-${pkgver}/src/{bitcoind,bitcoin-cli} -t ${pkgdir}/usr/bin/

    install -Dm644 ${startdir}/bitcoin.conf -t ${pkgdir}/etc/$pkgname/
    install -m644 ${pkgbase}-${pkgver}/doc/man/{bitcoind.1,bitcoin-cli.1} -t ${pkgdir}/usr/share/man/man1/
    install -m644 ${pkgbase}-${pkgver}/COPYING -t ${pkgdir}/usr/share/licenses/${pkgname}/
    install -m644 ${startdir}/bitcoind.service ${pkgdir}/usr/lib/systemd/system/
}
